
; --------------------------------------------------------------------------

STAGE_NO	DC.W	STAGE

		IF	BATCD_SHELL
BATCD_SCORE	EQU	$6F80
BATCD_LIFE	EQU	$6F84
		ELSE
BATCD_SCORE	DC.L	$00000000
BATCD_LIFE	DC.W	$0003
		ENDIF

; --------------------------------------------------------------------------

MAIN
		IF	BATCD_SHELL
		TST.B	shl_music
		BEQ.S	.done
		IF	LEVEL=1
		MOVE.W	#drive_1_music,MUSIC_DATA1
		ELSEIF	LEVEL=2
		MOVE.W	#drive_2_music,MUSIC_DATA1
		ELSEIF	LEVEL=3
		MOVE.W	#drive_3_music,MUSIC_DATA1
		ELSEIF	LEVEL=4
		MOVE.W	#drive_4_music,MUSIC_DATA1
		ELSEIF	LEVEL=5
		MOVE.W	#drive_5_music,MUSIC_DATA1
		ENDIF
		JSR	MUSIC_ROUT
.done		ENDIF

		IF	SOUND_INC
		JSR	PCM_DrvInit
		ENDIF

; --------------------------------------------------------------------------

MAIN_RESET
		IF	SOUND_INC
		JSR	STOP_ALL_SFX
		ENDIF

		JSR	DATA_RESET
		BSR	GLIST_RESET

		BSET	#4,SMEM_MODE
		MOVE.W	#%100,SROT_SIZE	;Set stamp size.
 	    	MOVE.W	#24-1,SROT_VCELL
 	    	MOVE.W	#SMAP_ADDR_HARD,SROT_MAP

; --------------------------------------------------------------------------

		MOVE.W	BATCD_LIFE,SRAM_DATA+WRAM_LIFE
		MOVE.L	BATCD_SCORE,SRAM_DATA+WRAM_SCORE

		MOVE.W	#$100+CAR_ZF,CAMERA_Z
		MOVE.L	#96*$100,CAMERA_Y

		MOVE.W	#$12,SCOM_SDATA+SCOM_PFADER

		MOVE.W	#1,TIMER_HOLD
		MOVE.W	#-1,STAGE_AUTO
		MOVE.L	#STAGE_CMD_LIST,STAGE_CLIST

		MOVE.W	#5,MISSTOT

; --------------------------------------------------------------------------

		LEA	GLIST+BMNUMS,A1
		BSR	ALLOCATE
		LEA	GLIST+BMFONT,A1
		BSR	ALLOCATE

		LEA	OBJ_MOBILE,A6
		BSR	O_OBJECT_SETUP

		JSR	RUPD_INIT
		NOT.W	SCL_SWAPIT
		BSR	CDLD_CONTROL
		CLR.W	SCL_SWAPIT

; --------------------------------------------------------------------------

MAIN_LOOP	BSR	SFX_FLAG_CLEAR
		BSR	IMG_BUFF_ROTATE
		BSR	PTWAVE_UPDATE

		MOVE.W	SCOM_MDATA+MCOM_JOY1,JOY1
		MOVE.W	SCOM_MDATA+MCOM_JOY0,D0

		IF	BATCD_SHELL
		LEA	JOY0_REDEFINE,A0
		MOVEQ	#0,D1
		MOVE.B	shl_drive_ctrl,D1
		LSL.W	#3,D1
		ADD.W	D1,A0
		MOVE.W	D0,D1
		AND.W	#%01110000,D1
		EOR.W	D1,D0
		LSR.W	#4,D1
		OR.B	(A0,D1.W),D0
		ENDIF

		MOVE.W	D0,JOY0
		CMP.W	#$F0,D0
		BNE.S	.pause_chk
.qwait		TST.W	SCOM_MDATA+MCOM_JOY0
		BNE.S	.qwait
		IF	BATCD_SHELL
		MOVE.W	#quit_game,SHELL_DATA
		BRA	GOTO_SHELL
		ELSE
		TST.W	PAUSE_FLAG
		BEQ.S	.sfxpclr
		MOVE.L	#SCMD_PAUSEOFF,-(SP)
		JSR	SEND_EVENT
.sfxpclr	MOVE.W	#STAGE,STAGE_NO
		MOVE.W	#3,BATCD_LIFE
		BRA	MAIN_RESET
		ENDIF

.pause_chk	TST.W	PAUSE_FLAG
		BNE	DO_PAUSE

		MOVE.W	SCOM_SDATA+SCOM_PFADER,D1
		CMP.W	PFADER_TO,D1
		BEQ.S	.pfdone
		BPL.S	.pfup
		ADDQ.W	#4,D1
.pfup		SUBQ.W	#2,D1
		MOVE.W	D1,SCOM_SDATA+SCOM_PFADER
.pfdone
		JSR	RSC_PRINT_INIT
		BSR	PRINT_PROCESS
		BSR	MISSTOT_PRINT
		BSR	TIMER_PRINT
		BSR	TEXT_PRINT

		MOVE.W	PX_YPOS,SRAM_DATA+WRAM_PX_YPOS
		MOVE.L	PX_XPOS,SRAM_DATA+WRAM_PX_XPOS

		BSR	OBJECT_CTRL

		BSR	CALC_CAMERA
		JSR	MOVERS
	 	JSR	PRINTS
		BSR	OBJECT_PRINT
		BSR	FLASH_DEALL_CHK

		BSR	SWAP_SET
		BSR	CDLD_CONTROL

		IF	TESTMODE
		IF	CD_DA
		MOVE.W	#CDBSTAT,D0
		JSR	_CDBIOS
		MOVE.W	24(A0),D1
		MOVE.W	JOY1,D0
		LSR.W	#3,D0
		BCC.S	.vskp0
		SUB.W	#10,D1
		BGT.S	.vskp1
		MOVEQ	#1,D1
		BRA.S	.vskp1
.vskp0		LSR.W	D0
		BCC.S	.vskp1
		ADD.W	#10,D1
		CMP.W	#$400,D1
		BMI.S	.vskp1
		MOVE.W	#$400,D1
.vskp1		OR.W	#$8000,D1
		MOVE.W	#FDRSET,D0
		JSR	_CDBIOS
		ENDIF

		IF	TESTPR
		LEA	OBJECT_DATA,A1
		MOVEQ	#OBJECT_MAX-1,D7
		MOVEQ	#0,D0
.loop		TST.B	OBJ_FLAG0(A1)
		BEQ.S	.Zskp
		ADDQ.W	#1,D0
.Zskp		LEA	OBJ_DLEN(A1),A1
		DBRA	D7,.loop
		MOVE.W	D0,SRAM_DATA+WRAM_TEST1
		ENDIF

		IF	GTESTPR
		BSR	ACCESS_REQ
		LEA	0*GR_DLEN*16+GLIST+2,A0
		LEA	EXPLDA+GLIST,A0
		LEA	SRAM_DATA+WRAM_GTEST0,A1
		MOVEQ	#16-1,D7
.tloop		MOVE.W	GR_ACTIVE(A0),(A1)+
		LEA	GR_DLEN(A0),A0
		DBRA	D7,.tloop
		SUBQ.W	#1,ACC_COUNT
		ENDIF
		ENDIF

		BSR	SWAP_WAIT

		IF	SLOWMOTION
		TST.B	SCOM_MDATA+MCOM_JOY0+1
		BPL.S	.skp
		MOVE.L	SVBL_CNT,D0
		ADD.L	#16,D0
.wait		CMP.L	SVBL_CNT,D0
		BCC.S	.wait
.skp		ELSE
		BSR	PAUSE_CHECK
		ENDIF

		BSR	STAGE_CONTROL
	
		IF	LEVEL=5
		BSR	CHEAT_MESS
		ENDIF

		ADDQ.L	#1,FRAME_CNT
		BRA	MAIN_LOOP

; --------------------------------------------------------------------------

		IF	LEVEL=5

CHEAT_MESS	CMP.L	#$00000500,BATCD_SCORE
		BNE	.end

		MOVE.W	CM_JOYVAL,D1
		MOVE.W	SCOM_MDATA+MCOM_JOY1,D0
		MOVE.W	D0,CM_JOYVAL
		BEQ	.end
		CMP.W	D1,D0
		BEQ	.end

		MOVE.L	CM_ADDR1,A0
		MOVE.L	#CM_DAT1,CM_ADDR1
		MOVE.L	CM_ADDR2,A1
		MOVE.L	#CM_DAT2,CM_ADDR2
		MOVE.L	CM_ADDR3,A2
		MOVE.L	#CM_DAT3,CM_ADDR3
		MOVE.L	CM_ADDR4,A3
		MOVE.L	#CM_DAT4,CM_ADDR4
		MOVE.L	CM_ADDR5,A4
		MOVE.L	#CM_DAT5,CM_ADDR5
		MOVE.L	CM_ADDR6,A5
		MOVE.L	#CM_DAT6,CM_ADDR6

.next1		CMP.B	(A0)+,D0
		BNE.S	.next2
		TST.B	(A0)+
		BEQ.S	.update1
		MOVE.W	(A0)+,PTIME
		MOVE.L	(A0)+,PTEXT
		RTS
.update1	MOVE.L	A0,CM_ADDR1

.next2		CMP.B	(A1)+,D0
		BNE.S	.next3
		TST.B	(A1)+
		BEQ.S	.update2
		MOVE.W	(A1)+,PTIME
		MOVE.L	(A1)+,PTEXT
		RTS
.update2	MOVE.L	A1,CM_ADDR2

.next3		CMP.B	(A2)+,D0
		BNE.S	.next4
		TST.B	(A2)+
		BEQ.S	.update3
		MOVE.W	(A2)+,PTIME
		MOVE.L	(A2)+,PTEXT
		RTS
.update3	MOVE.L	A2,CM_ADDR3

.next4		CMP.B	(A3)+,D0
		BNE.S	.next5
		TST.B	(A3)+
		BEQ.S	.update4
		MOVE.W	(A3)+,PTIME
		MOVE.L	(A3)+,PTEXT
		RTS
.update4	MOVE.L	A3,CM_ADDR4

.next5		CMP.B	(A4)+,D0
		BNE.S	.next6
		TST.B	(A4)+
		BEQ.S	.update5
		MOVE.W	(A4)+,PTIME
		MOVE.L	(A4)+,PTEXT
		RTS
.update5	MOVE.L	A4,CM_ADDR5

.next6		CMP.B	(A5)+,D0
		BNE.S	.next7
		TST.B	(A5)+
		BEQ.S	.update6
		MOVE.W	(A5)+,PTIME
		MOVE.L	(A5)+,PTEXT
		RTS
.update6	MOVE.L	A5,CM_ADDR6

.next7
.end		RTS

CM_JOYVAL	DC.W	0
CM_ADDR1	DC.L	CM_DAT1
CM_ADDR2	DC.L	CM_DAT2
CM_ADDR3	DC.L	CM_DAT3
CM_ADDR4	DC.L	CM_DAT4
CM_ADDR5	DC.L	CM_DAT5
CM_ADDR6	DC.L	CM_DAT6

CM_DAT1		DC.B	%10000000,0
		DC.B	%01000000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%10000000,0
		DC.B	%00100000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%01000000,0
		DC.B	%01000000,0
		DC.B	%01000000,1
		DC.W	80
		DC.L	CM_TEXT1

CM_DAT2		DC.B	%10000000,0
		DC.B	%01000000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%10000000,0
		DC.B	%00100000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%01000000,0
		DC.B	%01000000,0
		DC.B	%00100000,1
		DC.W	80
		DC.L	CM_TEXT2

CM_DAT3		DC.B	%10000000,0
		DC.B	%01000000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%10000000,0
		DC.B	%00100000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%01000000,0
		DC.B	%01000000,0
		DC.B	%00010000,1
		DC.W	80
		DC.L	CM_TEXT3

CM_DAT4		DC.B	%10000000,0
		DC.B	%01000000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%10000000,0
		DC.B	%00100000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%00100000,0
		DC.B	%00100000,0
		DC.B	%00100000,1
		DC.W	80
		DC.L	CM_TEXT4

CM_DAT5		DC.B	%10000000,0
		DC.B	%01000000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%10000000,0
		DC.B	%00100000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%00100000,0
		DC.B	%00100000,0
		DC.B	%00010000,1
		DC.W	80
		DC.L	CM_TEXT5

CM_DAT6		DC.B	%10000000,0
		DC.B	%01000000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%10000000,0
		DC.B	%00100000,0
		DC.B	%10000000,0
		DC.B	%00010000,0
		DC.B	%00010000,0
		DC.B	%00010000,0
		DC.B	%00010000,1
		DC.W	80
		DC.L	CM_TEXT6

CM_TEXT1	DC.B	P_SETWAVE
		DC.B	P_SETY,50+(32*0)
		DC.B	P_CENTER
		DC.B	"CONGRATS TO",0
		DC.B	P_SETY,50+(32*1)
		DC.B	P_CENTER
		DC.B	"LORRAINE N CRAIG",0
		DC.B	P_SETY,50+(32*2)
		DC.B	P_CENTER
		DC.B	"HAVE A GREAT LIFE",0
		DC.B	P_END

CM_TEXT2	DC.B	P_SETWAVE
		DC.B	P_SETY,50+(32*0)
		DC.B	P_CENTER
		DC.B	"HERES TO LOTS",0
		DC.B	P_SETY,50+(32*1)
		DC.B	P_CENTER
		DC.B	"OF WEIRDNESS",0
		DC.B	P_SETY,50+(32*2)
		DC.B	P_CENTER
		DC.B	"JENISE",0
		DC.B	P_END

CM_TEXT3	DC.B	P_SETWAVE
		DC.B	P_SETY,50+(32*0)
		DC.B	P_CENTER
		DC.B	"IN THE HOUSE",0
		DC.B	P_SETY,50+(32*1)
		DC.B	P_CENTER
		DC.B	"MOM DAD PETER",0
		DC.B	P_SETY,50+(32*2)
		DC.B	P_CENTER
		DC.B	"KATHRINA FIONA",0
		DC.B	P_SETY,50+(32*3)
		DC.B	P_CENTER
		DC.B	"ANNY AND AUDREY",0
		DC.B	P_END

CM_TEXT4	DC.B	P_SETWAVE
		DC.B	P_SETY,50+(32*0)
		DC.B	P_CENTER
		DC.B	"A BIG HELLO TO",0
		DC.B	P_SETY,50+(32*1)
		DC.B	P_CENTER
		DC.B	"DEIRDRE AND RAJIVE",0
		DC.B	P_SETY,50+(32*2)
		DC.B	P_CENTER
		DC.B	"CAREING PEOPLE",0
		DC.B	P_END

CM_TEXT5	DC.B	P_SETWAVE
		DC.B	P_SETY,50+(32*0)
		DC.B	P_CENTER
		DC.B	"MY MOUNTAIN CHILD",0
		DC.B	P_SETY,50+(32*1)
		DC.B	P_CENTER
		DC.B	"DO YOU KNOW",0
		DC.B	P_SETY,50+(32*2)
		DC.B	P_CENTER
		DC.B	"WHO YOU ARE",0
		DC.B	P_END

CM_TEXT6	DC.B	P_SETWAVE
		DC.B	P_SETY,50+(32*0)
		DC.B	P_CENTER
		DC.B	"IRISH FUNSTERS",0
		DC.B	P_SETY,50+(32*1)
		DC.B	P_CENTER
		DC.B	"MOSSY JERRY RO",0
		DC.B	P_SETY,50+(32*2)
		DC.B	P_CENTER
		DC.B	"GREG AND FINBAR",0
		DC.B	P_END

		EVEN

		ENDIF

; --------------------------------------------------------------------------

JOY0_REDEFINE	DC.B	%000<<4,%001<<4,%010<<4,%011<<4,%100<<4,%101<<4,%110<<4,%111<<4
		DC.B	%000<<4,%010<<4,%001<<4,%011<<4,%100<<4,%110<<4,%101<<4,%111<<4
		DC.B	%000<<4,%001<<4,%100<<4,%101<<4,%010<<4,%011<<4,%110<<4,%111<<4
		DC.B	%000<<4,%100<<4,%001<<4,%101<<4,%010<<4,%110<<4,%011<<4,%111<<4
		DC.B	%000<<4,%010<<4,%100<<4,%110<<4,%001<<4,%011<<4,%101<<4,%111<<4
		DC.B	%000<<4,%100<<4,%010<<4,%110<<4,%001<<4,%101<<4,%011<<4,%111<<4

; --------------------------------------------------------------------------

CALC_CAMERA
		BRA	.mskp

		MOVE.W	JOY1,D0
		MOVE.L	CAMERA_X,D1
.chk_r		LSR.W	D0
		BCC.S	.chk_l
		SUB.L	#$1000,D1
		CMP.L	#-400*$100,D1
		BPL.S	.chk_l
		MOVE.L	#-400*$100,D1
.chk_l		LSR.W	D0
		BCC.S	.set_cx
		ADD.L	#$1000,D1
		CMP.L	#400*$100,D1
		BMI.S	.set_cx
		MOVE.L	#400*$100,D1
.set_cx		MOVE.L	D1,CAMERA_X

		MOVE.W	CAMERA_Z,D2
		MOVE.L	CAMERA_Y,D1
.chk_d		LSR.W	D0
		BCC.S	.chk_u
		SUB.W	#$60,D2
		SUB.L	#$1000,D1
		CMP.L	#96*$100,D1
		BPL.S	.chk_u
		MOVE.W	#$100+CAR_ZF,D2
		MOVE.L	#96*$100,D1
.chk_u		LSR.W	D0
		BCC.S	.set_cy
		ADD.W	#$60,D2
		ADD.L	#$1000,D1
		CMP.L	#400*$100,D1
		BMI.S	.set_cy
		MOVE.W	#$60*$13+$100+CAR_ZF,D2
		MOVE.L	#400*$100,D1
.set_cy		MOVE.L	D1,CAMERA_Y
;		MOVE.W	D2,CAMERA_Z

		MOVE.W	CAMERA_Z,D1
.chk_b		LSR.W	D0
		BCC.S	.chk_f
		ADD.W	#$40*7,D1
.chk_f		LSR.W	D0
		BCC.S	.set_cz
		SUB.W	#$40*7,D1
.set_cz		MOVE.W	D1,CAMERA_Z


.mskp
		MOVE.W	CAMERA_Z,D0
		MOVE.L	CAMERA_X,D1
		MOVE.L	CAMERA_Y,D2

		MOVE.L	CAMERA_VIBE,D6
		BEQ.S	.pcalc
		MOVE.L	D6,D7
		BSR	RANDOM_RANGE_S
		ADD.L	D7,D1
		MOVE.L	D6,D7
		BSR	RANDOM_RANGE_U
		ADD.L	D7,D2
		SUB.L	#$180,D6
		BPL.S	.vset
		MOVEQ	#0,D6
.vset		MOVE.L	D6,CAMERA_VIBE

.pcalc		LEA	OBJECT_DATA,A0
		NEG.W	D0
		ADD.W	OBJ_ZPOS(A0),D0
		MOVE.W	D0,ZMOVE
		SUB.L	OBJ_XPOS(A0),D1
		MOVE.L	D1,XVIEW
		MOVE.L	OBJ_YPOS(A0),D0
		ASR.L	#1,D0
		SUB.L	D0,D2
		MOVE.L	YWATER,D0
		ASR.L	#1,D0
		SUB.L	D0,D2
		MOVE.L	D2,YVIEW
		RTS

; --------------------------------------------------------------------------

TIMER_PRINT	LEA	TIMER_BCD,A1
		MOVE.W	#$800,D0
		MOVE.W	#128-16,D4

		MOVE.B	(A1),D1
		BEQ.S	.tp_m
		BSR	.scale
		ADD.W	#16,D4
		MOVE.W	TIMER_BCD,D1
		LSR.B	#4,D1
		BRA.S	.tp_pm
.tp_m		ADDQ.W	#8,D4
		MOVE.W	(A1),D1
		LSR.B	#4,D1
		BEQ.S	.tp_e
.tp_pm		BSR	.scale
		ADDQ.W	#8,D4
.tp_e		ADDQ.W	#8,D4
		MOVE.W	(A1),D1
		TST.W	TIMER_HOLD
		BNE.S	.scale
		CMP.W	#10,D1
		BPL.S	.scale
		MOVE.W	D1,D0
		ADD.W	#4,D0
		LSL.W	#7,D0

.scale		MOVEM.L	A1/D0/D4,-(SP)
		LEA	NUMS_GRS,A0
		AND.W	#$F,D1
		LSL.W	#4,D1
		LEA	(A0,D1.W),A0

		MOVE.W	#4,D5
		MOVE.L	SCL_HEIGHT(A0),D6
		MOVE.L	SCL_WIDTH(A0),D7
		DIVU	D0,D6
		DIVU	D0,D7
		SWAP	D6
		MOVE.W	D7,D6
		ASR.W	D7
		SUB.W	D7,D4
		MOVE.L	#$01000000,D0
		MOVE.L	#$00C00000,D7
		BSR	SCALE
		MOVEM.L	(SP)+,A1/D0/D4
		RTS

NUMS_GRS	SCALER	(24*0),0,20,23,BMNUMS
		SCALER	(24*1),0,20,23,BMNUMS
		SCALER	(24*2),0,20,23,BMNUMS
		SCALER	(24*3),0,20,23,BMNUMS
		SCALER	(24*4),0,20,23,BMNUMS
		SCALER	(24*0),24,20,23,BMNUMS
		SCALER	(24*1),24,20,23,BMNUMS
		SCALER	(24*2),24,20,23,BMNUMS
		SCALER	(24*3),24,20,23,BMNUMS
		SCALER	(24*4),24,20,23,BMNUMS

; --------------------------------------------------------------------------

MISSTOT_PRINT
		MOVE.W	MISSTOT,D7
		BEQ.S	.end
		MOVE.W	#4,D5
		BRA.S	.start

.loop		MOVEM.W	D4-D7,-(SP)
		LEA	MISSPR_GRS,A0
		MOVE.L	#$000E000B,D6
		MOVE.L	#$01000000,D0
		MOVE.L	#$00C00000,D7
		BSR	SCALE
		MOVEM.W	(SP)+,D4-D7
		ADD.W	#10,D4
		DBRA	D6,.next
		ADD.W	#13,D5
.start		MOVE.W	#4,D4
		MOVE.W	#3-1,D6
.next		DBRA	D7,.loop
.end		RTS

MISSPR_GRS	SCALER	0,48,11,14,BMNUMS

; --------------------------------------------------------------------------

TIMER		MOVE.W	#3-1,D7
.add_lp		LEA	TIMER_ADD,A0
		TST.W	(A0)
		BEQ.S	.tdec
		BSR.S	DEC_BCD
		LEA	TIMER_BCD,A0
		BSR.S	INC_BCD
		LEA	TIMER_ADD,A0
		DBRA	D7,.add_lp
		RTS

.tdec		TST.W	TIMER_HOLD

		IF	TIME
		BNE.S	.end
		ELSE
		BRA.S	.end
		ENDIF

		LEA	TIMER_BCD,A0
		TST.W	(A0)
		BEQ.S	.end
		SUBQ.W	#1,2(A0)
		BPL.S	.end
		MOVE.W	#19,2(A0)
		BSR.S	DEC_BCD
		TST.W	(A0)
		BNE.S	.end
		MOVE.L	#STAGE_TU_LIST,STAGE_CLIST
		MOVE.W	#-1,STAGE_AUTO
		CLR.W	STAGE_PAUSE
.end		RTS

DEC_BCD		MOVEQ	#1,D0
		MOVE.W	#0,CCR
		MOVE.B	1(A0),D1
		SBCD	D0,D1
		MOVE.B	D1,1(A0)
		MOVEQ	#0,D0
		MOVE.B	(A0),D1
		SBCD	D0,D1
		MOVE.B	D1,(A0)
		RTS

INC_BCD		MOVEQ	#1,D0
		MOVE.W	#0,CCR
		MOVE.B	1(A0),D1
		ABCD	D0,D1
		MOVE.B	D1,1(A0)
		MOVEQ	#0,D0
		MOVE.B	(A0),D1
		ABCD	D0,D1
		MOVE.B	D1,(A0)
		RTS

; --------------------------------------------------------------------------

SCORE_ADD	LEA	SCORE_AMT+4,A1
SCADD		LEA	BATCD_SCORE+4,A2
		MOVE.W	#0,CCR
		ABCD.B	-(A1),-(A2)
		ABCD.B	-(A1),-(A2)
		ABCD.B	-(A1),-(A2)
		ABCD.B	-(A1),-(A2)
		MOVE.L	BATCD_SCORE,SRAM_DATA+WRAM_SCORE
		RTS

SCADD_ENEMY	LEA	SCAMT_ENEMY+4,A1
		BRA.S	SCADD
SCADD_MIDBOSS	LEA	SCAMT_MIDBOSS+4,A1
		BRA.S	SCADD
SCADD_MAINBOSS	LEA	SCAMT_MAINBOSS+4,A1
		BRA.S	SCADD
SCADD_SMALLOBJ	LEA	SCAMT_SMALLOBJ+4,A1
		BRA.S	SCADD
SCADD_ROADOBJ	LEA	SCAMT_ROADOBJ+4,A1
		BRA.S	SCADD

SCAMT_ENEMY	DC.L	SCORE_ENEMY
SCAMT_MIDBOSS	DC.L	SCORE_MIDBOSS
SCAMT_MAINBOSS	DC.L	SCORE_ENDBOSS
SCAMT_SMALLOBJ	DC.L	SCORE_SMALLOBJ
SCAMT_ROADOBJ	DC.L	SCORE_ROADOBJ

SUB_BCD		MOVE.W	#0,CCR
		SBCD.B	-(A1),-(A2)
		SBCD.B	-(A1),-(A2)
		SBCD.B	-(A1),-(A2)
		SBCD.B	-(A1),-(A2)
		BCC.S	.end
		ADDQ.L	#4,A1
		ADDQ.L	#4,A2
		MOVE.W	#0,CCR
		ABCD.B	-(A2),-(A1)
		ABCD.B	-(A2),-(A1)
		ABCD.B	-(A2),-(A1)
		ABCD.B	-(A2),-(A1)
		CLR.L	(A2)
.end		RTS

; --------------------------------------------------------------------------

SCORE_TIME	EQU	100

		RSRESET
S_END		RS.L	1
S_PRINT		RS.L	1
S_PAUSE		RS.L	1
S_GO		RS.L	1
S_ECLSET	RS.L	1
S_TIMEWAIT	RS.L	1
S_BONUS		RS.L	1
S_FADEOUT	RS.L	1
S_LOSE		RS.L	1
S_ACT		RS.L	1
S_PCTRL_ON	RS.L	1
S_PCTRL_OFF	RS.L	1

STAGE_CONTROL
		BSR	TIMER
		BSR	ECL_CONTROL

		TST.W	STAGE_PAUSE
		BEQ.S	.do_cmd
		SUBQ.W	#1,STAGE_PAUSE
		RTS

.do_cmd		MOVE.L	STAGE_CLIST,A0

.read_cmd	MOVEQ	#0,D0
		MOVE.B	(A0)+,D0
		JSR	.sjtab(PC,D0.W)
		BRA.S	.read_cmd
.sjtab		BRA.L	.s_end
		BRA.L	.s_print
		BRA.L	.s_pause
		BRA.L	.s_go
		BRA.L	ECL_SET
		BRA.L	.s_timewait
		BRA.L	.s_bonus
		BRA.L	.s_fadeout
		BRA.L	.s_lose
		BRA.L	.s_act
		BRA.L	.s_pctrl_on
		BRA.L	.s_pctrl_off

.s_pctrl_on	MOVE.W	#1,STAGE_PCTRL
		RTS
.s_pctrl_off	CLR.W	STAGE_PCTRL
		RTS
.s_end		SUBQ.L	#1,A0
.s_hold		MOVE.L	A0,STAGE_CLIST
		ADDQ.L	#4,SP
		RTS
.s_print	MOVEQ	#0,D0
		MOVE.B	(A0)+,D0
		CMP.B	#-1,D0
		BNE.S	.sp_skp
		MOVEQ	#-1,D0
.sp_skp		MOVE.W	D0,PTIME
		MOVE.B	(A0)+,D0
		LSL.W	#8,D0
		MOVE.B	(A0)+,D0
		SWAP	D0
		MOVE.B	(A0)+,D0
		LSL.W	#8,D0
		MOVE.B	(A0)+,D0
		MOVE.L	D0,PTEXT
		RTS
.s_pause	MOVEQ	#0,D0
		MOVE.B	(A0)+,D0
		MOVE.W	D0,STAGE_PAUSE
		BRA.S	.s_hold
.s_go		CLR.W	TIMER_HOLD
		CLR.W	ECL_HOLD
		CLR.W	STAGE_AUTO
		CLR.W	STAGE_TONEXT
		MOVE.W	#1,ECL_PAUSE
		RTS
.s_timewait	TST.W	TIMER_ADD
		BNE.S	.s_end
		RTS

.s_bonus	LEA	STAGE_BONUS,A2
		LEA	SCORE_AMT,A1
		MOVE.L	(A2)+,D1
		LSR.L	#4,D1
		AND.L	#$FFFFFFF0,D1
		BNE.S	.cntdwn
		MOVE.L	#$10,D1
.cntdwn		MOVE.L	D1,(A1)+
		BSR	SUB_BCD
		BSR	SCORE_ADD
		TST.W	STAGE_HEAL
		BNE.S	.cntdwn_heal
		TST.L	STAGE_BONUS
		BNE	.s_end
		RTS
.cntdwn_heal	SUBQ.W	#1,STAGE_HEAL
		LEA	OBJECT_DATA,A1
		SUB.W	#$400,OBJ_DTOTAL(A1)
		BCC.S	.sbskp
		CLR.W	STAGE_HEAL
		CLR.W	OBJ_DTOTAL(A1)
.sbskp		ADDQ.B	#1,OBJ_FLAG3(A1)
		BRA	.s_end

.s_fadeout	MOVE.W	#$10,PFADER_TO
		RTS
.s_lose		TST.W	SCOM_MDATA+MCOM_FBUSY
		BNE.S	.s_lose

		ADDQ.L	#8,SP

		LEA	BATCD_LIFE+2,A0
		LEA	SCORE_AMT,A1
		MOVE.W	#1,(A1)+
		MOVE.W	#0,CCR
		SBCD	-(A1),-(A0)
		SBCD	-(A1),-(A0)
		BCC	MAIN_RESET

		IF	BATCD_SHELL
		MOVE.W	#gameover,SHELL_DATA
		BRA.S	GOTO_SHELL
		ELSE
		MOVE.W	#3,BATCD_LIFE
		MOVE.W	#STAGE,STAGE_NO
		BRA	MAIN_RESET
		ENDIF

.s_act		TST.W	SCOM_MDATA+MCOM_FBUSY
		BNE.S	.s_act
		IF	BATCD_SHELL
		MOVE.W	#load_next,SHELL_DATA
		ELSE
		MOVE.W	#STAGE,STAGE_NO
		ADDQ.L	#8,SP
		BRA	MAIN_RESET
		ENDIF

GOTO_SHELL	IF	SOUND_INC
		JSR	STOP_ALL_SFX
		ENDIF
		MOVE.L	SVBL_CNT,D0
		ADD.L	#10,D0
.wait		CMP.L	SVBL_CNT,D0
     		BCC.S	.wait
		MOVE.W	#-1,SCOM_SDATA+SCOM_FRAME
		BRA	*

STAGE_ACT_LIST
		DC.B	S_PCTRL_OFF
		DC.B	S_PRINT,-1
		DC.L	TEXT01
		DC.B	S_PAUSE,20
		DC.B	S_PRINT,-1
		DC.L	TEXT04
		DC.B	S_PAUSE,10
		DC.B	S_BONUS
		DC.B	S_PRINT,2*20
		DC.L	TEXT04
		DC.B	S_PAUSE,2*20+5
		DC.B	S_PRINT,-1
		DC.L	TEXT07
		DC.B	S_PAUSE,2*20+5
		DC.B	S_FADEOUT
		DC.B	S_PAUSE,7
		DC.B	S_ACT
STAGE_CLR_LIST
		DC.B	S_PCTRL_OFF
		DC.B	S_PRINT,-1
		DC.L	TEXT01
		DC.B	S_PAUSE,20
		DC.B	S_PRINT,-1
		DC.L	TEXT04
		DC.B	S_PAUSE,10
		DC.B	S_BONUS
		DC.B	S_PRINT,2*20
		DC.L	TEXT04
		DC.B	S_PAUSE,2*20+5
STAGE_CMD_LIST
		DC.B	S_PCTRL_OFF
		DC.B	S_PRINT,-1
		DC.L	TEXT00
		DC.B	S_PAUSE,5
		DC.B	S_ECLSET
		DC.B	S_TIMEWAIT
		DC.B	S_PRINT,20
		DC.L	TEXT02
		DC.B	S_PAUSE,20
		DC.B	S_PRINT,20
		DC.L	TEXT05
		DC.B	S_GO
		DC.B	S_PAUSE,20
		DC.B	S_PCTRL_ON
		DC.B	S_END
STAGE_TU_LIST
		DC.B	S_PCTRL_OFF
		DC.B	S_PAUSE,5
		DC.B	S_PRINT,-1
		DC.L	TEXT06
		DC.B	S_PAUSE,50
		DC.B	S_PCTRL_OFF
		DC.B	S_FADEOUT
		DC.B	S_PAUSE,7
		DC.B	S_LOSE

STAGE_DEAD_LIST
		DC.B	S_PRINT,0
		DC.L	0
		DC.B	S_PAUSE,50
		DC.B	S_PCTRL_OFF
		DC.B	S_FADEOUT
		DC.B	S_PAUSE,7
		DC.B	S_LOSE

		EVEN

; --------------------------------------------------------------------------

		RSRESET
ECL_NOP		RS.L	1
ECL_NEXT	RS.L	1
ECL_ACTIVE_CHK	RS.L	1
ECL_FWAIT	RS.L	1
ECL_STAGE_CLR	RS.L	1
ECL_ACT_CLR	RS.L	1
ECL_STAGE_CLRX	RS.L	1
ECL_BOSSMUSIC	RS.L	1
ECL_ENEMYS	RS.L	1

ECL_SET		LEA	ECL_DATA,A2	;Setup stage control.
		MOVE.W	STAGE_NO,D0
		LSL.W	#3,D0
		LEA	-8(A2,D0.W),A2
		LEA	TIMER_ADD,A3
		LEA	TIMER_BCD+2,A4
		MOVE.W	(A2)+,(A3)+
		AND.W	#0,CCR
		SBCD	-(A4),-(A3)
		SBCD	-(A4),-(A3)
		BCC.S	.ok
		CLR.W	(A3)
.ok		MOVE.W	(A2)+,D0
		MOVE.W	D0,ECL_COUNT
.set		MOVE.W	D0,SRAM_DATA+WRAM_EDAMAGE
.skp		MOVE.L	(A2)+,ECL_CMD_DATA
		MOVE.W	#19,TIMER_TICK
		RTS

ECL_KILL	ADDQ.W	#1,ECL_PAUSE
		TST.W	ECL_ACTIVE
		BEQ.S	.skp
		SUBQ.W	#1,ECL_ACTIVE
.skp		TST.W	ECL_COUNT
		BEQ.S	.end
		SUBQ.W	#1,ECL_COUNT
		MOVE.W	ECL_COUNT,SRAM_DATA+WRAM_EDAMAGE
.end		RTS

ECL_CONTROL	CMP.W	#-1,OBJECT_DATA+OBJ_DTOTAL
		BEQ.S	.end
		TST.W	ECL_HOLD
		BNE.S	.end
		TST.W	ECL_PAUSE
		BEQ.S	.end
		SUBQ.W	#1,ECL_PAUSE
		BNE.S	.end
		MOVE.L	ECL_CMD_DATA,A0
.next		MOVE.L	A0,ECL_CMD_DATA
		MOVEQ	#0,D0
		MOVE.B	(A0)+,D0
		JMP	.jtab(PC,D0.W)
.fwait		MOVE.W	ECL_FLAGWAIT,D0
		CMP.B	(A0)+,D0
		BEQ.S	.next
		ADDQ.W	#1,ECL_PAUSE
		SUBQ.L	#2,A0
.fnop		MOVE.L	A0,ECL_CMD_DATA
		RTS
.active		TST.W	ECL_ACTIVE
		BEQ.S	.next
.end		RTS
.jtab		BRA.L	.fnop
		BRA.L	CECL_NEXT
		BRA.L	.active
		BRA.L	.fwait
		BRA.L	CECL_STAGE_CLR
		BRA.L	CECL_ACT_CLR
		BRA.L	CECL_STAGE_CLRX
		BRA.L	.cecl_bossmusic
		BRA.L	.cecl_enemys

.cecl_enemys	MOVEQ	#0,D0
		MOVE.B	(A0)+,D0
		MOVE.W	D0,ECL_COUNT
		MOVE.W	D0,SRAM_DATA+WRAM_EDAMAGE
		BRA.S	.next

.cecl_bossmusic	IF	BATCD_SHELL
		TST.B	shl_music
		BEQ.S	.next
		MOVE.L	A0,-(SP)
		MOVE.W	#drive_boss_music,MUSIC_DATA1
		JSR	MUSIC_ROUT
		MOVE.L	(SP)+,A0
		ENDIF
		BRA	.next

CECL_NEXT	MOVEQ	#0,D0
		MOVE.B	(A0)+,D0
		MOVE.W	D0,-(SP)
		MOVE.B	(A0)+,D0
		LSL.W	#8,D0
		MOVE.B	(A0)+,D0
		SWAP	D0
		MOVE.B	(A0)+,D0
		LSL.W	#8,D0
		MOVE.B	(A0)+,D0
		MOVE.L	A0,-(SP)
		MOVE.L	D0,A0

		IF	ENEMYS=YO
		JSR	(A0)
		BEQ.S	.ok
.error		ADDQ.L	#6,SP
		ADDQ.W	#1,ECL_PAUSE
		RTS
		ENDIF

.ok		MOVE.L	(SP)+,ECL_CMD_DATA
		MOVE.W	(SP)+,D0
		ADD.W	D0,ECL_PAUSE
		ADDQ.W	#1,ECL_ACTIVE
		RTS

CECL_ACT_CLR  	MOVE.L	#STAGE_ACT_LIST,STAGE_CLIST
		MOVE.W	#1,STAGE_AUTO
		BRA.S	CECL_STGCLR_SET

CECL_STAGE_CLRX	MOVE.L	#STAGE_CLR_LIST,STAGE_CLIST
		MOVE.W	#2,STAGE_AUTO
		MOVE.W	#$A60,STAGE_GOTO
		BRA.S	CECL_STGCLR_SET

CECL_STAGE_CLR 	MOVE.L	#STAGE_CLR_LIST,STAGE_CLIST
		MOVE.W	#1,STAGE_AUTO

CECL_STGCLR_SET	ADDQ.W	#1,STAGE_NO
		ADDQ.W	#1,STAGE_TONEXT
		MOVE.W	#1,TIMER_HOLD
		CLR.W	STAGE_PAUSE

		MOVE.W	#32,STAGE_HEAL
		IF	BATCD_SHELL
		TST.B	shl_difficulty
		BNE.S	.skp
		MOVE.W	#64,STAGE_HEAL
.skp		ENDIF

		LEA	STAGE_BONUS+1,A1
		MOVE.W	TIMER_BCD,D0
		MOVE.W	D0,D1
		MOVE.W	D0,D2
		LSR.W	#8,D0
		LSR.W	#4,D1
		AND.W	#$F,D1
		AND.W	#$F,D2
		MULU	#100,D0
		MULU	#10,D1
		ADD.W	D1,D0
		ADD.W	D2,D0
		MOVE.W	D0,D1

		MULU	#SCORE_TIME,D0
		DIVU	#10000,D0
		MOVE.B	D0,(A1)+
		CLR.W	D0
		SWAP	D0
		DIVU	#1000,D0
		MOVE.W	D0,D1
		CLR.W	D0
		SWAP	D0
		DIVU	#100,D0
		LSL.B	#4,D1
		OR.B	D0,D1
		MOVE.B	D1,(A1)+
		CLR.W	D0
		SWAP	D0
		DIVU	#10,D0
		MOVE.W	D0,D1
		SWAP	D0
		LSL.B	#4,D1
		OR.B	D0,D1
		MOVE.B	D1,(A1)
		RTS

; --------------------------------------------------------------------------

TEXT00		DC.B	P_SETY,48
		DC.B	P_CENTER
		DC.B	"STAGE ",PNUM,1-1
		DC.L	STAGE_NO+1
		DC.B	0
		DC.B	P_END

TEXT01		DC.B	P_SETY,48
		DC.B	P_CENTER
		DC.B	"STAGE CLEAR",0
		DC.B	P_END

TEXT02		DC.B	P_SUB
		DC.L	TEXT00
		DC.B	P_SETY,88
		DC.B	P_CENTER
		DC.B	"GET READY",0
		DC.B	P_END

TEXT03		DC.B	P_SUB
		DC.L	TEXT01
		DC.B	P_SETY,80
		DC.B	P_CENTER
		DC.B	"TIME BONUS",0
		DC.B	P_END

TEXT04		DC.B	P_SUB
		DC.L	TEXT03
		DC.B	P_SETXY,120,80+32
		DC.B	P_CENTER
		DC.B	PNUMEQ,5-1
		DC.L	STAGE_BONUS+1
		DC.B	0
		DC.B	P_END

TEXT05		DC.B	P_FLASH
		DC.B	P_SETZ
		DC.W	$400
		DC.B	P_SETY,80
		DC.B	P_CENTER
		DC.B	"GO",0
		DC.B	P_END

TEXT06		DC.B	P_SETY,98
		DC.B	P_CENTER
		DC.B	"TIME UP",0
		DC.B	P_END

TEXT07		DC.B	P_SETZ
		DC.W	$400
		DC.B	P_SETY,60
		DC.B	P_CENTER
		DC.B	"ACT",0
		DC.B	P_SETY,100
		DC.B	P_CENTER
		DC.B	"CLEAR",0
		DC.B	P_END

		EVEN

PNUM		EQU	1
PNUMEQ		EQU	2

		RSRESET
P_END		RS.L	1
P_CENTER	RS.L	1
P_LINE		RS.L	1
P_SETX		RS.L	1
P_SETY		RS.L	1
P_SETZ		RS.L	1	
P_SETWAVE	RS.L	1	
P_FLASH		RS.L	1	
P_SETXY		RS.L	1	
P_SUB		RS.L	1	

TEXT_PRINT		
;		LEA	TEXT03,A1
;		BRA.S	PRN_TEXT

		MOVE.W	PTIME,D0
		BEQ.S	.end
		BMI.S	.skp
		SUBQ.W	#1,PTIME
.skp		MOVE.L	PTEXT,A1
		BSR	PRN_TEXT
.end		RTS
      		

PRN_TEXT  	CLR.W	TWAVE_FLAG
		MOVE.W	#$800,D0
.read_cmd	MOVEQ	#0,D1
		MOVE.B	(A1)+,D1
		JSR	.ptjtab(PC,D1.W)
		BRA.S	.read_cmd
.ptjtab		BRA.L	.prn_end
		BRA.L	PRN_CENTER
		BRA.L	PRN_LINE
		BRA.L	.set_x
		BRA.L	.set_y
		BRA.L	.set_z
		BRA.L	.set_wave
		BRA.L	.flash
		BRA.L	.set_xy
		BRA.L	.p_sub

.flash		BTST	#1,FRAME_CNT+3
		BEQ.S	.ret  
.prn_end	ADDQ.L	#4,SP
.ret		RTS
.set_x		MOVEQ	#0,D4
		MOVE.B	(A1)+,D4
		RTS
.set_xy		MOVEQ	#0,D4
		MOVE.B	(A1)+,D4
.set_y		MOVEQ	#0,D5
		MOVE.B	(A1)+,D5
		RTS
.set_z		MOVE.B	(A1)+,D0
		LSL.W	#8,D0
		MOVE.B	(A1)+,D0
		RTS
.set_wave	MOVE.W	#1,TWAVE_FLAG
		RTS
.p_sub		MOVE.B	(A1)+,D1
		LSL.W	#8,D1
		MOVE.B	(A1)+,D1
		SWAP	D1
		MOVE.B	(A1)+,D1
		LSL.W	#8,D1
		MOVE.B	(A1)+,D1
		MOVE.L	A1,-(SP)
		MOVE.L	D1,A1
		BSR.S	.read_cmd
		MOVE.L	(SP)+,A1
		RTS

PRN_CENTER	MOVE.L	A1,-(SP)
		LEA	PSCALC,A6
		MOVEQ	#0,D4
.lp		BSR	PSREAD
		BNE.S	.lp
		NEG.W	D4
		ADD.W	#256,D4
		ASR.W	D4
		MOVE.L	(SP)+,A1
		
PRN_LINE	LEA	PSCALE,A6
.lp		BSR	PSREAD
		BNE.S	.lp
   		RTS

PSREAD		LEA	BMF_A,A0
		MOVEQ	#0,D1
		MOVE.B	(A1)+,D1
		BEQ.S	.end
		CMP.W	#PNUM,D1
		BEQ.S	.pnum
		CMP.W	#PNUMEQ,D1
		BEQ.S	.pnum_eq
		SUB.W	#"A",D1
		BPL.S	.print
		LEA	BMF_0,A0
		ADD.W	#"A"-"0",D1
		BPL.S	.print
		MOVE.L	#$8*$800,D1
		DIVU  	D0,D1
		ADD.W	D1,D4
		MOVE.W	#0,CCR
.end		RTS
.print		LSL.W	#4,D1
		LEA	(A0,D1.W),A0	
		JMP	(A6)

.pnum_eq	LEA	BMF_0EQ,A0
		BRA.S	.do_pnum
.pnum		LEA	BMF_0,A0
.do_pnum	MOVEQ	#0,D7
		MOVE.B	(A1)+,D7
		MOVE.B	(A1)+,D1
		LSL.W	#8,D1
		MOVE.B	(A1)+,D1
		SWAP	D1
		MOVE.B	(A1)+,D1
		LSL.W	#8,D1
		MOVE.B	(A1)+,D1
		MOVE.L	D1,A2
		BTST	#0,D7
		BEQ.S	.pn_lzpr	
.pn_lzpl	MOVEQ	#0,D1
		MOVE.B	(A2),D1
		LSR.B	#4,D1
		BNE.S	.pn_plp
		SUBQ.W	#1,D7
.pn_lzpr	TST.W	D7
		BEQ.S	.pn_pr
		MOVE.B	(A2)+,D1
		AND.W	#$F,D1
		BNE.S	.pn_prp
		DBRA	D7,.pn_lzpl
		MOVE.W	#0,CCR
		RTS
.pn_pl		MOVEQ	#0,D1
		MOVE.B	(A2),D1
		LSR.B	#4,D1
.pn_plp		MOVEM.L	A0/A2/D7,-(SP)
		BSR	.print
		MOVEM.L	(SP)+,A0/A2/D7
		SUBQ.W	#1,D7
.pn_pr		MOVE.B	(A2)+,D1
		AND.W	#$F,D1
.pn_prp		MOVEM.L	A0/A2/D7,-(SP)
		BSR	.print
		MOVEM.L	(SP)+,A0/A2/D7
		DBRA	D7,.pn_pl
		MOVE.W	#0,CCR
		RTS

PSCALC		MOVE.L	SCL_WIDTH(A0),D1
		DIVU	D0,D1
		ADD.W	D1,D4
		MOVE.W	#0,CCR
		RTS

PSCALE		MOVE.L	SCL_HEIGHT(A0),D6
		MOVE.L	SCL_WIDTH(A0),D7
		DIVU	D0,D6
		DIVU	D0,D7
		MOVEM.L	A1/D0/D4/D5/D7,-(SP)
		
		BSR	PTWAVE
		
		SWAP	D6
		MOVE.W	D7,D6
		MOVE.L	#$01000000,D0
		MOVE.L	#$00C00000,D7
		BSR	SCALE
		MOVEM.L	(SP)+,A1/D0/D4/D5/D7
		ADD.W	D7,D4
		MOVE.W	#0,CCR
		RTS

PTWAVE_UPDATE	MOVE.W	#2,D1
		MOVE.W	TWAVE_CNT0A,D2
		MOVE.W	TWAVE_CNT1A,D3
		BMI.S	.cneg
		NEG.W	D1
.cneg		ADD.W	D1,D2
		ADD.W	D2,D3
		MOVE.W	D2,TWAVE_CNT0A
		MOVE.W	D3,TWAVE_CNT1A
		MOVE.W	D2,TWAVE_CNT0B
		MOVE.W	D3,TWAVE_CNT1B
		RTS

PTWAVE		TST.W	TWAVE_FLAG
		BEQ.S	.end

		MOVE.W	#2,D1
		MOVE.W	TWAVE_CNT0B,D2
		MOVE.W	TWAVE_CNT1B,D3
		BMI.S	.cneg
		NEG.W	D1
.cneg		ADD.W	D1,D2
		ADD.W	D2,D3
		MOVE.W	D2,TWAVE_CNT0B
		MOVE.W	D3,TWAVE_CNT1B
		ADD.W	D3,D5
.end		RTS

TWAVE_FLAG	DC.W	0
TWAVE_CNT0A	DC.W	0
TWAVE_CNT1A	DC.W	8
TWAVE_CNT0B	DC.W	0
TWAVE_CNT1B	DC.W	0

BMF_A		SCALER	0,0,18,16,BMFONT
BMF_B		SCALER	18,0,15,16,BMFONT
BMF_C		SCALER	33,0,16,16,BMFONT
BMF_D		SCALER	49,0,18,16,BMFONT
BMF_E		SCALER	67,0,15,16,BMFONT
BMF_F		SCALER	82,0,15,16,BMFONT
BMF_G		SCALER	97,0,17,16,BMFONT
BMF_H		SCALER	114,0,16,16,BMFONT
BMF_I		SCALER	130,0,8,16,BMFONT
BMF_J		SCALER	138,0,13,16,BMFONT
BMF_K		SCALER	151,0,16,16,BMFONT
BMF_L		SCALER	167,0,15,16,BMFONT
BMF_M		SCALER	0,16,21,16,BMFONT
BMF_N		SCALER	21,16,15,16,BMFONT
BMF_O		SCALER	36,16,18,16,BMFONT
BMF_P		SCALER	54,16,14,16,BMFONT
BMF_Q		SCALER	68,16,20,16,BMFONT
BMF_R		SCALER	88,16,16,16,BMFONT
BMF_S		SCALER	104,16,15,16,BMFONT
BMF_T		SCALER	119,16,17,16,BMFONT
BMF_U		SCALER	136,16,16,16,BMFONT
BMF_V		SCALER	152,16,18,16,BMFONT
BMF_W		SCALER	170,16,21,16,BMFONT
BMF_X		SCALER	0,32,16,16,BMFONT
BMF_Y		SCALER	16,32,16,16,BMFONT
BMF_Z		SCALER	32,32,16,16,BMFONT
BMF_0		SCALER	0,48,15,16,BMFONT
BMF_1		SCALER	19,48,9,16,BMFONT
BMF_2		SCALER	34,48,12,16,BMFONT
BMF_3		SCALER	50,48,12,16,BMFONT
BMF_4		SCALER	66,48,12,16,BMFONT
BMF_5		SCALER	82,48,12,16,BMFONT
BMF_6		SCALER	97,48,13,16,BMFONT
BMF_7		SCALER	114,48,12,16,BMFONT
BMF_8		SCALER	129,48,13,16,BMFONT
BMF_9		SCALER	145,48,13,16,BMFONT
BMF_0EQ		SCALER	0,48,15,16,BMFONT
BMF_1EQ		SCALER	16,48,15,16,BMFONT
BMF_2EQ		SCALER	32,48,15,16,BMFONT
BMF_3EQ		SCALER	48,48,15,16,BMFONT
BMF_4EQ		SCALER	64,48,15,16,BMFONT
BMF_5EQ		SCALER	80,48,15,16,BMFONT
BMF_6EQ		SCALER	96,48,15,16,BMFONT
BMF_7EQ		SCALER	112,48,15,16,BMFONT
BMF_8EQ		SCALER	128,48,15,16,BMFONT
BMF_9EQ		SCALER	144,48,15,16,BMFONT

; --------------------------------------------------------------------------

DO_PAUSE	BSR	COPY_FRAME
		MOVE.L	#SCL_BUFFER,SCL_MAKE_AT
		MOVE.L	#SCL_BUFFER,SCL_PRINT_AT

		BSR	PRINT_PAUSED
		BSR	SWAP_SET
		BSR	CDLD_CONTROL
		BSR	SWAP_WAIT
		TST.B	PAUSE_FLAG+1
		BEQ.S	.pchk_set
		BSR	PAUSE_CHECK
		BEQ.S	.pchk_skp
.pchk_set	MOVE.B	SCOM_MDATA+MCOM_JOY0+1,PAUSE_KEY
.pchk_skp
		IF	CD_DA
		TST.B	shl_music
		BEQ.S	.skp
		CLR.B	PAUSE_FLAG
		MOVE.W	#CDBSTAT,D0
		JSR	_CDBIOS
		MOVE.W	(A0),D0
		AND.W	#$7F00,D0
		BNE.S	.skp
		NOT.B	PAUSE_FLAG
.skp		ENDIF

		IF	BATCD_SHELL
		TST.B	shl_cheat2
		BEQ.S	.pause_chk
		MOVE.B	PAUSE_KEY,D0
		AND.W	#%01110000,D0
		CMP.B	#%00010000,D0
		BNE.S	.pause_chk
.qwait		TST.W	SCOM_MDATA+MCOM_JOY0
		BNE.S	.qwait
		MOVE.W	#load_next,SHELL_DATA
		BRA	GOTO_SHELL
		ENDIF

.pause_chk	TST.W	PAUSE_FLAG
		BNE	MAIN_LOOP

PAUSE_CLEAR	IF	SOUND_INC
		MOVE.L	#SCMD_PAUSEOFF,-(SP)
		JSR	SEND_EVENT
		ENDIF

		LEA	GLIST+BMCDPA,A1
		BSR	DEALLOCATE
		LEA	GLIST+BMFONT,A1
		BSR	DEALLOCATE
		JSR	PRINTS
		BRA	MAIN_LOOP


PAUSE_CHECK	MOVE.B	PAUSE_KEY,D0
		MOVE.B	SCOM_MDATA+MCOM_JOY0+1,D1
		MOVE.B	D1,PAUSE_KEY
		NOT.B	D0
		AND.B	D1,D0
		AND.B	#%10000000,D0
		BNE.S	.pmode
.end		RTS

.pmode		NOT.B	PAUSE_FLAG+1
		BEQ.S	.pause_off

.pause_on	TST.W	STAGE_PCTRL
		BNE.S	.setup
		CLR.B	PAUSE_FLAG+1
		RTS

.setup		IF	SOUND_INC
		MOVE.L	#SCMD_PAUSEON,-(SP)
		JSR	SEND_EVENT
		ENDIF

		IF	CD_DA
		TST.B	shl_music
		BEQ.S	.fwait
		MOVE.W	#MSCPAUSEON,D0
		JSR	_CDBIOS
		ENDIF

.fwait		TST.W	SCOM_MDATA+MCOM_FBUSY
		BNE.S	.fwait
		CLR.W	PAUSE_SHOW_FLAG		;'PAUSED' scale setup.
	 	MOVE.W	#4,D7
		BSR	RANDOM_RANGE_U
		MULU	#36,D7
		LEA	PRP_Z_LIST,A0
		LEA	(A0,D7.W),A0
		LEA	PRP_Z_DATA,A1
		MOVEQ	#6-1,D7
.lp		MOVE.L	(A0)+,(A1)+
		MOVE.W	(A0)+,(A1)+
		DBRA	D7,.lp
		LEA	GLIST+BMCDPA,A1
		BSR	ALLOCATE
		LEA	GLIST+BMFONT,A1
		BSR	ALLOCATE
		BRA.S	COPY_FRAME

.pause_off 	IF	CD_DA
		TST.B	shl_music
		BEQ	.end
		MOVE.W	#MSCPAUSEOFF,D0
		JSR	_CDBIOS
		ENDIF

		RTS

		IF	SOUND_INC
SCMD_PAUSEON	DC.B	1,SRT_STOP
SCMD_PAUSEOFF	DC.B	1,SRT_CONTINUE
		ENDIF

COPY_FRAME	LEA	IMG_BADDR,A0
		MOVEQ	#0,D0
		MOVE.W	(A0)+,D0
		LSL.L	#2,D0
		ADD.L	#SWORD_RAM,D0
		MOVEQ	#0,D1
		MOVE.W	(A0)+,D1
		LSL.L	#2,D1
		ADD.L	#SWORD_RAM,D1
		MOVE.L	D0,A0
		MOVE.L	D1,A1
		MOVE.W	#$600-1,D7
		BSR	ACCESS_REQ
.tr_lp		MOVE.L	(A0)+,(A1)+
		MOVE.L	(A0)+,(A1)+
		MOVE.L	(A0)+,(A1)+
		MOVE.L	(A0)+,(A1)+
		DBRA	D7,.tr_lp
		BRA	ACCESS_OFF

PRINT_PAUSED	MOVE.B	SCOM_MDATA+MCOM_JOY0+1,D1
		AND.B	#%01110000,D1
		CMP.B	#%01110000,D1
		BEQ.S	.skchk
		MOVEQ	#0,D1
.skchk		MOVE.B	PAUSE_SHOW_KEY,D0
		MOVE.B	D1,PAUSE_SHOW_KEY
		NOT.B	D0
		AND.B	D1,D0
		AND.B	#%01110000,D0
		BEQ.S	.show
		NOT.W	PAUSE_SHOW_FLAG

.show		TST.W	PAUSE_SHOW_FLAG
		BNE.S	.end

		TST.W	PAUSE_FLAG		;CD PAUSED
		BPL.S	.norm
		LEA	PGRS_CD,A0
		MOVE.W	#$108,D0
		MOVE.W	PAUSECD_RVAL,D2
		ADD.W	#28,D2
		AND.W	#$3FE,D2
		MOVE.W	D2,PAUSECD_RVAL
		BSR	PRN_ROTATE
		LEA	PGRS_CDA,A0
		NOT.W	PAUSECD_FVAL
		BNE.S	.fskp
		LEA	PGRS_CDB,A0
.fskp		MOVE.W	#$108,D0
		BSR	PRN_SCALE
		TST.B	PAUSE_FLAG+1
		BEQ.S	.end

.norm		LEA	PRP_Z_DATA,A1
		MOVEQ	#6-1,D7

.loop		MOVE.L	(A1)+,A0
		MOVE.W	(A1),D0
		ADD.W	#$30,D0
		CMP.W	#$180,D0
		BMI.S	.ok
		MOVE.W	#$180,D0
.ok		MOVE.W	D0,(A1)+
		BLE.S	.skp
		MOVEM.L	D7/A1,-(SP)
		BSR	PRN_SCALE
		MOVEM.L	(SP)+,D7/A1
.skp		DBRA	D7,.loop

.end		RTS

; --------------------------------------------------------------------------

PRN_SCALE	MOVEM.L	(A0)+,D4-D7	;Xoff/Yoff/Xsize/Ysize
		DIVS	D0,D4
		DIVS	D0,D5
		DIVS	D0,D6
		BNE.S	.xok
		MOVEQ	#1,D6
.xok		DIVS	D0,D7
		BNE.S	.yok
		MOVEQ	#1,D7
.yok		ADD.W	#256/2,D4
		ADD.W	#1+RSHEIGHT/2,D5
		ASL.W	D4
		SUB.W	D6,D4
		ASR.W	D4
		ASL.W	D5
		SUB.W	D7,D5
		ASR.W	D5
		SWAP	D6
		MOVE.W	D7,D6
		SWAP	D6
		MOVE.L	#$00C00000,D7
		MOVE.L	#$01000000,D0
		BRA	SCALE

; --------------------------------------------------------------------------

;D0=Zpos.
;D2=Angle[0-$3FE]

PRN_ROTATE	MOVEM.L	(A0)+,D4-D7	;D4=Xpos D5=Ypos D6=width D7=Height 

		DIVS	D0,D4
		DIVS	D0,D5
		DIVS	D0,D6
		BNE.S	.xok
		MOVEQ	#1,D6
.xok		DIVS	D0,D7
		BNE.S	.yok
		MOVEQ	#1,D7
.yok 		SWAP	D6
		MOVE.W	D7,D6
		SWAP	D6
		MOVE.L	D6,D3
	  	ASR.W	D7
		SUB.W	D7,D5
	 	MOVE.W	D6,D7
	  	ASR.W	D7
	   	SUB.W	D7,D4

		ADD.W	#256/2,D4
		ADD.W	#1+RSHEIGHT/2,D5

		LEA	SIN_LOOKUP,A4
		MOVE.W	(A4,D2.W),D1
		SWAP	D1
		LEA	$100(A4),A4
		MOVE.W	(A4,D2.W),D1

		MOVE.L	#$00C00000,D7
		MOVE.L	#$01000000,D0
		BRA	SCALE_ROTATE

; --------------------------------------------------------------------------

PRP_Z_LIST   	
		DC.L	PGRS_S
   		DC.W	$00 
		DC.L	PGRS_D
     		DC.W	$60		
		DC.L	PGRS_A
 		DC.W	-$60 
		DC.L	PGRS_E
    		DC.W	-$C0 
		DC.L	PGRS_P
		DC.W	-$120 
		DC.L	PGRS_U
  		DC.W	-$180 

		DC.L	PGRS_U
		DC.W	$00 
		DC.L	PGRS_S
		DC.W	$00 
		DC.L	PGRS_A
		DC.W	-$A0 
		DC.L	PGRS_E
		DC.W	-$A0 
		DC.L	PGRS_P
		DC.W	-$140		
		DC.L	PGRS_D
		DC.W	-$140 

		DC.L	PGRS_P
		DC.W	$60		
		DC.L	PGRS_A
		DC.W	$00 
		DC.L	PGRS_U
		DC.W	-$60 
		DC.L	PGRS_S
		DC.W	-$C0 
		DC.L	PGRS_E
		DC.W	-$120 
		DC.L	PGRS_D
		DC.W	-$180 

		DC.L	PGRS_D
     		DC.W	$60		
		DC.L	PGRS_E
    		DC.W	$00 
		DC.L	PGRS_S
   		DC.W	-$60 
		DC.L	PGRS_U
  		DC.W	-$C0 
		DC.L	PGRS_A
 		DC.W	-$120 
		DC.L	PGRS_P
		DC.W	-$180 

		XYRATIO	100,150,100,150
PGRS_P		XYSIZE	-39,0
		XYSIZE	14,16
		SCALER	54,16,14,16,BMFONT
PGRS_A		XYSIZE	-25,0
		XYSIZE	18,16
		SCALER	0,0,18,16,BMFONT
PGRS_U		XYSIZE	-9,0
		XYSIZE	18,16
		SCALER	136,16,16,16,BMFONT
PGRS_S		XYSIZE	8,0
		XYSIZE	15,16
		SCALER	104,16,15,16,BMFONT
PGRS_E		XYSIZE	23,0
		XYSIZE	15,16
		SCALER	67,0,15,16,BMFONT
PGRS_D		XYSIZE	40,0
		XYSIZE	18,16
		SCALER	49,0,18,16,BMFONT

		XYRATIO	100,100,100,100
PGRS_CD		XYSIZE	0,0
		XYSIZE	64,64
		SCALER	64,64,-64,64,BMCDPA

PGRS_CDA	XYSIZE	0,0
		XYSIZE	64,64
		SCALER	128,0,64,64,BMCDPA
PGRS_CDB	XYSIZE	0,0
		XYSIZE	64,64
		SCALER	128,64,64,64,BMCDPA

; ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
; บ SFX3D PREFORMAT AND CONTROL						   บ
; ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ

		IF	SOUND_INC

EC_DATA		DS.W	6

SFX3D_ZXREL	LEA	EC_DATA,A0

		MOVE.W	OBJ_ZPOS(A1),D0		;Calc Zrel.
		CMP.W	#$100,D0
		BPL.S	.fchk
		MOVE.W	#$100,D0
.fchk		MOVE.W	D0,D2
		CMP.W	#VIEW_PATCHES*$80,D0
		BMI.S	.calc
		MOVE.W	#VIEW_PATCHES*$80,D0

.calc		BSR	O_CALC_PATCHZ
		MOVE.L	RCXPOS(A5),D1
		ADD.L	OBJ_XPOS(A1),D1
		DIVS	D2,D1
		SUB.W	#$100,D2
		MOVE.W	D2,EC_ZREL(A0)

		ASL.W	#2,D1
		BPL.S	.pos
		CMP.W	#-$1F0,D1
		BPL.S	.set
		MOVE.W	#-$1F0,D1
		BRA.S	.set
.pos		CMP.W	#$1F0,D1
		BMI.S	.set
		MOVE.W	#$1F0,D1
.set		NEG.W	D1
		MOVE.W	D1,EC_XREL(A0)
		RTS


SFX3D_SPEED	IF	BATCD_SHELL
		TST.B	shl_sfx
		BEQ.S	.end
		ENDIF

		BSR	SFX3D_ZXREL

		MOVE.L	OBJ_SPD(A1),D0		;Calc speed.
		BPL.S	.skp
		MOVEQ	#0,D0
.skp		LSL.L	#1,D0
		SWAP	D0
		MOVE.W	D0,EC_PARM0(A0)

		MOVE.W	OBJ_FX0_SFXNUM(A1),EC_SFXNUM(A0)
		MOVE.W	OBJ_FX0_HANDLE(A1),EC_HANDLE(A0)
		JSR	CONTROL_SFX
		MOVE.W	EC_HANDLE(A0),OBJ_FX0_HANDLE(A1)
		MOVE.W	EC_SFXNUM(A0),OBJ_FX0_SFXNUM(A1)
.end		RTS

SFX3D_KILL	IF	BATCD_SHELL
		TST.B	shl_sfx
		BEQ.S	.end
		ENDIF

		LEA	EC_DATA,A0
		MOVE.W	OBJ_FX0_HANDLE(A1),EC_HANDLE(A0)
		MOVE.W	OBJ_FX0_SFXNUM(A1),EC_SFXNUM(A0)
		OR.W	#$8000,EC_SFXNUM(A0)
		JSR	CONTROL_SFX

.end		RTS

		ELSE
SFX3D_SPEED
SFX3D_KILL
CSFX_BATSKI
CSFX_BATMOBILE
CSFX_ONESHOT	RTS
		ENDIF

CSFX_ONESHOT_V	IF	BATCD_SHELL
		TST.B	shl_sfx
		BEQ.S	.end
		ENDIF

		MOVEM.L	A0/D0/D1/D2,-(SP)
		LEA	SFX_FLAG_DATA,A0
		MOVE.W	D0,D1
		AND.W	#$7F,D1
		TAS.B	(A0,D1.W)
		BNE.S	.done

		MOVEQ	#0,D2
		ROL.W	#8,D0
		MOVE.W	D0,D1
		SWAP	D0
		MOVE.W	D1,D0
		BPL.S	.go

		MOVE.W	OBJ_ZPOS(A1),D1
		SUB.W	#$80,D1
		CMP.W	#$1000,D1
		BCC.S	.done
		ADD.W	#$100,D1
		MOVE.L	#$8000,D2
		DIVU	D1,D2
		CMP.W	#50,D2
		BPL.S	.go
		MOVE.W	#50,D2

.go		MOVE.B	D2,D0
		ROL.W	#8,D0
		AND.L	#$FF7F7F,D0
	      	JSR	CSFX_ONESHOT
.done		MOVEM.L	(SP)+,A0/D0/D1/D2
.end		RTS

SFX_FLAG_CLEAR	LEA	SFX_FLAG_DATA,A0
		MOVEQ	#32-1,D7
.clr_lp		CLR.L	(A0)+
		CLR.L	(A0)+
		CLR.L	(A0)+
		CLR.L	(A0)+
		DBRA	D7,.clr_lp
		RTS

HORN_SFX	RTS

		CMP.W	#$400,OBJ_ZPOS(A1)
		BPL.S	.end
		MOVE.L	SVBL_CNT,D7
		SUB.L	HORN_TIME,D7
		CMP.L	#20*15,D7
		BMI.S	.end

		MOVE.W	#100,D7
		JSR	RANDOM_RANGE_U
		CMP.W	#95,D7
		BMI.S	.end
		JSR	CSFX_ONESHOT_V
		MOVE.L	SVBL_CNT,HORN_TIME

.end		RTS

; --------------------------------------------------------------------------

;		MOVE.W	.jval,D0
;		MOVE.W	JOY1,D1
;		MOVE.W	D1,.jval
;		NOT.W	D0	
;		AND.W	D1,D0
;		AND.W	#%10000000,D0
;		BEQ.S	.jskp
;		BRA.S	.jok
;.jval		DC.W	0
;.jok
;.jskp

